#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс	
// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - Настройки интеграции этого объекта.
//       Описание см. в модуле ПодключаемыеКомандыПереопределяемый,
//       комментарий к процедуре ПриОпределенииСоставаНастроекПодключаемыхОбъектов,
//       раздел "Параметры процедуры ПриОпределенииНастроек"
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
КонецПроцедуры	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Документ на печать                             
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "ЕАС_ИтоговыйОтчет";
	КомандаПечати.МенеджерПечати = "Документ.ЕАС_ИтоговыеОтчеты";
	КомандаПечати.Представление  = НСтр("ru = 'Итоговый отчет ЕАС'");
	КомандаПечати.СписокФорм 	 = "ФормаДокумента";
	
	КомандаПечати.ПроверкаПроведенияПередПечатью    = Истина;
	КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                   представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЕАС_ИтоговыйОтчет") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЕАС_ИтоговыйОтчет",
			НСтр("ru = 'Итоговый отчет ЕАС'"),
			СформироватьПечатнуюФормуИтоговыйОтчет(МассивОбъектов, ОбъектыПечати));
				
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьПечатнуюФормуИтоговыйОтчет(МассивОбъектов, ОбъектыПечати)
	Перем Результат;
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл
		
		Запрос = Новый Запрос;           
		Запрос.Текст = "ВЫБРАТЬ
		               |	ИтоговыеОтчеты.Ссылка КАК Ссылка,
		               |	ИтоговыеОтчеты.Шаблон КАК Шаблон,
		               |	ИтоговыеОтчеты.Дата КАК Дата,
		               |	ИтоговыеОтчеты.Номер КАК Номер,
		               |	ИтоговыеОтчеты.ДатаНачала КАК ДатаНачала,
		               |	ИтоговыеОтчеты.ДатаОкончания КАК ДатаОкончания,
		               |	МАКСИМУМ(ИтоговыеОтчетыПрисоединенныеФайлы.Ссылка) КАК ПрисоединенныйФайл
		               |ИЗ
		               |	Документ.ЕАС_ИтоговыеОтчеты КАК ИтоговыеОтчеты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕАС_ИтоговыеОтчетыПрисоединенныеФайлы КАК ИтоговыеОтчетыПрисоединенныеФайлы
		               |		ПО (ИтоговыеОтчетыПрисоединенныеФайлы.ВладелецФайла = ИтоговыеОтчеты.Ссылка)
		               |			И (ИтоговыеОтчетыПрисоединенныеФайлы.Шаблон = ИтоговыеОтчеты.Шаблон)
		               |			И (ИтоговыеОтчетыПрисоединенныеФайлы.Служебный)
		               |ГДЕ
		               |	ИтоговыеОтчеты.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ИтоговыеОтчеты.Ссылка,
		               |	ИтоговыеОтчеты.Шаблон,
		               |	ИтоговыеОтчеты.Дата,
		               |	ИтоговыеОтчеты.Номер,
		               |	ИтоговыеОтчеты.ДатаНачала,
		               |	ИтоговыеОтчеты.ДатаОкончания";
		Запрос.УстановитьПараметр("Ссылка", ОбъектПечати);
		
		РезультатЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока РезультатЗапроса.Следующий() Цикл
			ПрисоединенныйФайл = РезультатЗапроса.ПрисоединенныйФайл;
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);	
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("mxl");
			ДвоичныеДанные.Записать(ИмяВременногоФайла);
			Если Результат <> Неопределено Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;	
			Результат = Новый ТабличныйДокумент;
			Результат.Прочитать(ИмяВременногоФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
			УдалитьФайлы(ИмяВременногоФайла);
			
			ТабличныйДокумент.Вывести(Результат);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Результат <> Неопределено Тогда
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 1, ОбъектыПечати, МассивОбъектов[0]);
	КонецЕсли;	
	Возврат ТабличныйДокумент;
	
КонецФункции
	
#КонецОбласти
	
	
#КонецЕсли	
