#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс	
// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - Настройки интеграции этого объекта.
//       Описание см. в модуле ПодключаемыеКомандыПереопределяемый,
//       комментарий к процедуре ПриОпределенииСоставаНастроекПодключаемыхОбъектов,
//       раздел "Параметры процедуры ПриОпределенииНастроек"
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Документ на печать
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "ОтчетЕАС";
	КомандаПечати.МенеджерПечати = "Документ.ЕАС_ОтчетыИнтеграции";
	КомандаПечати.Представление  = НСтр("ru = 'Отчет ЕАС'");
	КомандаПечати.Обработчик     = "Форма.ПечатьОтчета";
	КомандаПечати.СписокФорм 	 = "ФормаДокумента";
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                   представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем ТабличныйДокументФормы,Шаблон,ЗаголовокФормы;
    ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ОтчетЕАС");
	Если ПечатнаяФорма <> Неопределено 
				И ТипЗнч(ПараметрыПечати) = Тип("Структура")     
				И ПараметрыПечати.Свойство("ЕАС_ТабличныйДокумент", ТабличныйДокументФормы) Тогда
		Если ПараметрыПечати.Свойство("ЕАС_Шаблон", Шаблон) Тогда
			ТабличныйДокументФормы.ИмяПараметровПечати = Строка(Шаблон);
		КонецЕсли;	
		Если ПараметрыПечати.Свойство("ЕАС_Шаблон", ЗаголовокФормы) Тогда
			ПечатнаяФорма.СинонимМакета = ЗаголовокФормы;
		Иначе			
	        ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Отчет ЕАС'" );
		КонецЕсли;	
        ПечатнаяФорма.ТабличныйДокумент = ТабличныйДокументФормы;
		
		Области = Новый Соответствие;
		Для Каждого ИмяОбласти Из СтрРазделить("Заголовок,Шапка,Строки,Подвал", ",") Цикл
			Если ТабличныйДокументФормы.Области.Найти(ИмяОбласти) <> Неопределено Тогда
				Области.Вставить(ИмяОбласти, ТабличныйДокументФормы.ПолучитьОбласть(ИмяОбласти));    
			КонецЕсли;	
		КонецЦикла;	
		
		Если Области.Количество() <= 1  Тогда
	        ПечатнаяФорма.ТабличныйДокумент = ТабличныйДокументФормы;
		Иначе	
			НомерЛиста = 1;
			ТабличныйДокумент = Новый ТабличныйДокумент;
			ТекущаяОбласть = Области.Получить("Заголовок");
			Если ТекущаяОбласть <> Неопределено Тогда 
				ТабличныйДокумент.Вывести(ТекущаяОбласть);
			КонецЕсли;	
			ОбластьШапка = Области.Получить("Шапка");
			Если ОбластьШапка <> Неопределено Тогда 
				ТабличныйДокумент.Вывести(ОбластьШапка);
			КонецЕсли;	
			
			СтрокаСПодвалом = Новый Массив;
			ОбластьПодвал = Области.Получить("Подвал");
			Если ОбластьПодвал <> Неопределено Тогда            
				СтрокаСПодвалом.Добавить(ОбластьПодвал);
			КонецЕсли;	
			
			ОбластьСтроки = Области.Получить("Строки");     
			Если ОбластьСтроки <> Неопределено Тогда
				НомерСтроки = 1; //ОбластьСтроки.Верх;
				Пока НомерСтроки <= ОбластьСтроки.ВысотаТаблицы Цикл
					ОбластьСтрока = ОбластьСтроки.ПолучитьОбласть(НомерСтроки, , НомерСтроки);
					СтрокаСПодвалом.Добавить(ОбластьСтрока);
					Если СтрокаСПодвалом.Количество() = 3 Тогда
						СтрокаСПодвалом.Удалить(2);
					КонецЕсли;	
					РезультатПроверки = ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом);
					Если НЕ РезультатПроверки Тогда
						// Вывод разделителя и заголовка таблицы на новой странице
						НомерЛиста = НомерЛиста + 1;
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();     
						Если ОбластьШапка <> Неопределено Тогда 
							ТабличныйДокумент.Вывести(ОбластьШапка);
						КонецЕсли;	
					КонецЕсли;	
					ТабличныйДокумент.Вывести(ОбластьСтрока);
			        НомерСтроки = НомерСтроки + 1;
				КонецЦикла;	
			КонецЕсли;
			
			Если ОбластьПодвал <> Неопределено Тогда 
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			КонецЕсли;	
	        ПечатнаяФорма.ТабличныйДокумент = ТабличныйДокумент;
		КонецЕсли;	
		
    КонецЕсли;
КонецПроцедуры

#КонецОбласти
	
	
#КонецЕсли	
