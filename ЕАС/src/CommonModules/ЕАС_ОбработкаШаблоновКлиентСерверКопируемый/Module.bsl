// Модуль написан с учетом того, что он может исполняться как в конфигурации БСХП
//  так и в ЕАС
// Все изменения требуется синхронизировать в обоих конфигурациях

#Область ПрограммныйИнтерфейс

// Устанавливает реквизит "Значение" в табличной части "РассчитанныеПоказатели" документа ЕАС_ОтчетыИнтеграции
//
// Параметры:
// РассчитанныеПоказатели - ТаблицаФормы - Табличная часть документа ЕАС_ОтчетыИнтеграции
// ОписаниеПоля - Структура - Содержащая реквизиты СправочникСсылка.ЕАС_ПоляФормШаблонов в качестве ключей,
// НовоеЗначение - Число - Устанавливаемое значение,
// УстановленоВручную - Булево - признак того что значение заносится вручную (не автоматически),
// УстанавливатьБезПроверки - Булево - устанавливается в любом случае (Если Ложь, тогда значение ранее установленное вручную, не изменяется на "автоматические (Не ручные)").
//
Процедура УстановитьПоказатель(РассчитанныеПоказатели, ОписаниеПоля, НовоеЗначение, УстановленоВручную = Ложь, УстанавливатьБезПроверки = Ложь) Экспорт 
	ПараметрыОтбора = Новый Структура("ПолеФормы", ОписаниеПоля.Ссылка);
	РассчитанныеСтроки = РассчитанныеПоказатели.НайтиСтроки(ПараметрыОтбора);
	Если РассчитанныеСтроки.Количество() > 0 Тогда
		РассчитаннаяСтрока = РассчитанныеСтроки[0];    
	Иначе
		РассчитаннаяСтрока = РассчитанныеПоказатели.Добавить();     
		РассчитаннаяСтрока.ПолеФормы = ОписаниеПоля.Ссылка;
	КонецЕсли;	
	
	Если УстанавливатьБезПроверки Или УстановленоВручную Или Не РассчитаннаяСтрока.УстановленоВручную Тогда
		// Правим только не установленные вручную
		РассчитаннаяСтрока.Значение = НовоеЗначение;
		РассчитаннаяСтрока.УстановленоВручную = УстановленоВручную;
	КонецЕсли;	
КонецПроцедуры                   

// Устанавливает реквизит "Значение" в табличной части "РассчитанныеПоказатели" документа ЕАС_ОтчетыИнтеграции.
//
// Параметры:
// РассчитанныеПоказатели - ТаблицаФормы - Табличная часть документа ЕАС_ОтчетыИнтеграции,
// ОписаниеПоля - Структура - Структура, содержащая реквизиты СправочникСсылка.ЕАС_ПоляФормШаблонов в качестве ключей.
//
// Возвращаемое значение:
//   Число - Установленное значение реквизита "Значение".
//
Функция ПолучитьПоказатель(РассчитанныеПоказатели, ОписаниеПоля) Экспорт 
	ПараметрыОтбора = Новый Структура("ПолеФормы", ОписаниеПоля.Ссылка);
	РассчитанныеСтроки = РассчитанныеПоказатели.НайтиСтроки(ПараметрыОтбора);
	Если РассчитанныеСтроки.Количество() > 0 Тогда
		Возврат РассчитанныеСтроки[0].Значение;           
	КонецЕсли;	
КонецФункции

// Возвращает Ссылку по имени поля (содержит символ "_"
//
// Параметры:
//   ИмяСтрока - Строка - Параметр или Имя ячейки.
//
// Возвращаемое значение: 
//   СправочникСсылка.ЕАС_ПоляФормШаблонов - если имя соответствует формату,
//   										Иначе Неопределено
//
Функция СсылкаЯчейкиШаблона(ИмяСтрока) Экспорт
	УИДЯчейки = УИДЯчейкиПоИмени(ИмяСтрока);
	Если УИДЯчейки <> Неопределено Тогда
		Возврат ЕАС_ОбщегоНазначенияВызовСервераКопируемый.СсылкаСправочникаПоУИД("ЕАС_ПоляФормШаблонов", УИДЯчейки);
	Иначе 	
		УИДЯчейки = УИДЯчейкиПоПараметру(ИмяСтрока);
		Если УИДЯчейки <> Неопределено Тогда
			Возврат ЕАС_ОбщегоНазначенияВызовСервераКопируемый.СсылкаСправочникаПоУИД("ЕАС_ПоляФормШаблонов", УИДЯчейки);
		КонецЕсли;
	КонецЕсли;	
КонецФункции

// Возвращает Имя ячейки табличного документа по ссылке ЕАС_ПоляФормШаблонов
//
// Параметры:
//   Ссылка - СправочникСсылка.ЕАС_ПоляФормШаблонов - элемент справочника.
//
// Возвращаемое значение: 
//   - Строка - Сформированное имя ячейки табличного документа
//
Функция ИмяЯчейкиПоСсылке(Ссылка) Экспорт
	Если ЗначениеЗаполнено(Ссылка) Тогда
		УИДСтрока = ЕАС_ОбщегоНазначенияВызовСервераКопируемый.УИДСтрокойПоСсылке(Ссылка);
		Возврат СтрЗаменить(УИДСтрока, "-","_");
	КонецЕсли;	
КонецФункции

// Возвращает Имя параметра ячейки табличного документа по ссылке ЕАС_ПоляФормШаблонов
//
// Параметры:
//  Ссылка - СправочникСсылка.ЕАС_ПоляФормШаблонов - элемент справочника.
//
// Возвращаемое значение: 
//   - Строка - Сформированное имя параметра ячейки табличного документа.
//
Функция ИмяПараметраЯчейкиПоСсылке(Ссылка) Экспорт
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ИмяЯчейки = ИмяЯчейкиПоСсылке(Ссылка);
		Возврат ИмяПараметраЯчейкиПоИмени(ИмяЯчейки);
	КонецЕсли;	
КонецФункции

// Возвращает уникальной идентификатор сформированный из имени ячейки табличного документа
//
// Параметры:
//   ИмяЯчейки - Строка - имя ячейки (области) табличного документа.
//
// Возвращаемое значение: 
//   - Строка - Сформированное строковое значение уникального идентификатора.
//
Функция УИДЯчейкиПоИмени(ИмяЯчейки) Экспорт
	УИДЯчейки = СтрЗаменить(ИмяЯчейки, "_", "-");
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(УИДЯчейки) Тогда
		Возврат УИДЯчейки;
	КонецЕсли;	
КонецФункции

// Возвращает Имя параметра ячейки по имени Ячейки табличного документа
//
// Параметры:
//   ИмяЯчейки - Строка - имя ячейки (области) табличного документа.
//
// Возвращаемое значение: 
//   - Строка - Сформированное имя параметра ячейки табличного документа.
//
Функция ИмяПараметраЯчейкиПоИмени(ИмяЯчейки) Экспорт
	УИДЯчейки = УИДЯчейкиПоИмени(ИмяЯчейки);
	Если УИДЯчейки <> Неопределено Тогда
		Возврат "Ид_" + ИмяЯчейки;
	КонецЕсли;	
КонецФункции	

// Возвращает Имя ячейки табличного документа из имени параметра
//
// Параметры:
//   ИмяПараметра - Строка - имя параметра (области) табличного документа.
//
// Возвращаемое значение: 
//   - Строка - имя ячейки (области) табличного документа.
//
Функция ИмяЯчейкиПоПараметру(ИмяПараметра) Экспорт
	ИмяЯчейки = Сред(ИмяПараметра, 4); // отрезаем "Ид_"
	УИДЯчейки = УИДЯчейкиПоИмени(ИмяЯчейки);
	Если УИДЯчейки <> Неопределено Тогда
		Возврат ИмяЯчейки;   
	КонецЕсли;	
КонецФункции	

// Возвращает уникальной идентификатор сформированный из параметра ячейки табличного документа
//
// Параметры:
//  ИмяПараметр - Строка - имя параметра (области) табличного документа.
//
// Возвращаемое значение: 
//   - Строка - Сформированное строковое значение уникального идентификатора.
//
Функция УИДЯчейкиПоПараметру(ИмяПараметр) Экспорт
	ИмяЯчейки = ИмяЯчейкиПоПараметру(ИмяПараметр);
	Если ИмяЯчейки <> Неопределено Тогда
		Возврат УИДЯчейкиПоИмени(ИмяЯчейки);
	КонецЕсли;	
КонецФункции

// Возвращение наименование поля щаблона по координатам и заполненным полям обхект
// Параметры:
//  Верх - Число - Координата по вертикали
//  Лево - Число - Координата по горизонтали
//  ПолеОбъект - СправочникОбъект.ЕАС_ПоляФормШаблонов или Структура с аналогичными полями
//
// Возвращаемое значение: 
//   - Строка - Сформированное наименование

Функция НаименованиеПоляШаблона(Верх, Лево, ПолеОбъект) Экспорт
	КоординатыЯчейки = "S" + Формат(Верх, "ЧН=0; ЧГ=0") + ":" + Формат(Лево, "ЧН=0; ЧГ=0");
	Период = Новый СтандартныйПериод;
	ЗаполнитьЗначенияСвойств(Период, ПолеОбъект);
	Результат = КоординатыЯчейки 
				+ " - " + ПолеОбъект.ПоказательОтчета  
 				+ " - " + Период 
				+ " - " + ПолеОбъект.Источник;
	Если ЗначениеЗаполнено(ПолеОбъект.ДопПараметр) Тогда 
		Результат = Результат + " - " + ПолеОбъект.ДопПараметр;
	КонецЕсли;	                                     
	Возврат Результат;
КонецФункции

#КонецОбласти