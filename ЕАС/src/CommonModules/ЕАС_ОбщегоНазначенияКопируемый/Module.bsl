#Область ПрограммныйИнтерфейс

// Модуль написан с учетом того, что он может исполняться как в конфигурации БСХП
//  так и в ЕАС
// Все изменения требуется синхронизировать в обоих конфигурациях
//

// Возвращает присоединенные данные объекта (см. РегистрСведений.ЕАС_ПрисоединенныеДанные)
//
// Параметры:
// ВладелецДанных - ссылка - Владелец присоединенных данных,
// ВидДанных - ПеречислениеСсылка.ЕАС_ВидыПрисоединенныхДанных - Вид присоединенных данных.
//
// Возвращаемое значение:
//   ХранилищеЗначений - присоединенные данные.
//
Функция ПолучитьПрисоединенныеДанные(ВладелецДанных, ВидДанных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрисоединенныеДанные.ХранилищеДанных КАК ХранилищеДанных
		|ИЗ
		|	РегистрСведений.ЕАС_ПрисоединенныеДанные КАК ПрисоединенныеДанные
		|ГДЕ
		|	ПрисоединенныеДанные.ВладелецДанных = &ВладелецДанных
		|	И ПрисоединенныеДанные.ВидДанных = &ВидДанных";
	
	Запрос.УстановитьПараметр("ВидДанных",      Перечисления.ЕАС_ВидыПрисоединенныхДанных.ТабличныйДокумент);
	Запрос.УстановитьПараметр("ВладелецДанных", ВладелецДанных);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда 
		Возврат РезультатЗапроса.ХранилищеДанных;
	КонецЕсли;
	
КонецФункции

// Записывает присоединенные данные объекта (см. РегистрСведений.ЕАС_ПрисоединенныеДанные)
//
// Параметры:
// ВладелецДанных - ссылка - Владелец присоединенных данных
// ВидДанных - ПеречислениеСсылка.ЕАС_ВидыПрисоединенныхДанных - Вид присоединенных данных
// Данные - произвольный тип - сохраняемые данные. Если не задано - сохраненные ранее данные удаляются
//
Процедура ЗаписатьПрисоединенныеДанные(ВладелецДанных, ВидДанных, Данные) Экспорт
	
	Если ТипЗнч(Данные) = Тип("ТабличныйДокумент") Тогда
		НетДанных = Данные.Области.Количество() = 0;
	Иначе
		НетДанных = Не ЗначениеЗаполнено(Данные);
	КонецЕсли;	

	МенеджерЗапаси = РегистрыСведений.ЕАС_ПрисоединенныеДанные.СоздатьМенеджерЗаписи();
	МенеджерЗапаси.ВладелецДанных = ВладелецДанных;
	МенеджерЗапаси.ВидДанных      = ВидДанных;
	
	Если НетДанных Тогда	
		МенеджерЗапаси.Прочитать();                       
		Если МенеджерЗапаси.Выбран() Тогда
			МенеджерЗапаси.Удалить();
		КонецЕсли;
	Иначе	
		МенеджерЗапаси.ХранилищеДанных = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(9));
		МенеджерЗапаси.Записать();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти
