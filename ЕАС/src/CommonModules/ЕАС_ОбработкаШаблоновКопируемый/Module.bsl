// Модуль написан с учетом того, что он может исполняться как в конфигурации БСХП
//  так и в ЕАС
// Все изменения требуется синхронизировать в обоих конфигурациях

#Область ПрограммныйИнтерфейс
// Формирует результат запроса по подчиненным элементам в справочнике ЕАС_ПоляФормШаблонов
//
// Параметры:
// ТабличныйДокумент - ТабличныйДокумент - табличный документ сформированный по Шаблону,
// Шаблон - СправочникСсылка.ЕАС_ШаблоныОтчетов - служит для определения полей шаблона,
// ОписанияОбластейСсылок - Соответствие - заполняется описание полей шаблона:
// * Ключ -   СправочникСсылка.ЕАС_ПоляФормШаблонов - описываемо поле
// * Значение - Структура со свойствами:
// 	 ** Область - ОбластьЯчеекТабличногоДокумента - Ячейка (область ячеек) табличного документ,
// 	 ** ИмяПараметра - Строка - Имя параметра ячейки (области) табличного документа.
//
// Возвращаемое значение:
//   РезультатЗапроса - текущая область данных
//
Функция ЗапросПолейШаблона(ТабличныйДокумент, Шаблон, ОписанияОбластейСсылок = Неопределено) Экспорт
	
	// Выборка Параметров                 
	НомерОбласти = 0;
	Области = ТабличныйДокумент.Области;
	
	МассивСсылок = Новый Массив;
	ОписанияОбластейСсылок = Новый Соответствие;
	
	Пока НомерОбласти < Области.Количество() Цикл
		
		ОбластьЯчейка = Области[НомерОбласти];
		ОбластьИмя = ОбластьЯчейка.Имя;
		СсылкаПоляФормы = ЕАС_ОбработкаШаблоновКлиентСерверКопируемый.СсылкаЯчейкиШаблона(ОбластьИмя);
		Если СсылкаПоляФормы <> Неопределено Тогда
			ИмяПараметра = ЕАС_ОбработкаШаблоновКлиентСерверКопируемый.ИмяПараметраЯчейкиПоИмени(ОбластьИмя);
			МассивСсылок.Добавить(СсылкаПоляФормы); 
			ОписанияОбластейСсылок.Вставить(СсылкаПоляФормы, Новый Структура("Область, ИмяПараметра", ОбластьЯчейка, ИмяПараметра));
		КонецЕсли;	
				
		НомерОбласти = НомерОбласти + 1;
				                                                  
	КонецЦикла;
	
	Запрос = Новый Запрос;        
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоляШаблонов.Ссылка КАК Ссылка,
	               |	ПоляШаблонов.ВерсияДанных КАК ВерсияДанных,
	               |	ПоляШаблонов.ПометкаУдаления КАК ПометкаУдаления,
	               |	ПоляШаблонов.Владелец КАК Владелец,
	               |	ПоляШаблонов.Код КАК Код,
	               |	ПоляШаблонов.Наименование КАК Наименование,
	               |	ПоляШаблонов.ОбластьДанных КАК ОбластьДанных,
	               |	ПоляШаблонов.ПоказательОтчета КАК ПоказательОтчета,
	               |	ПоляШаблонов.Источник КАК Источник,
	               |	НАЧАЛОПЕРИОДА(ПоляШаблонов.ДатаНачала, ДЕНЬ) КАК ДатаНачала,
	               |	КОНЕЦПЕРИОДА(ПоляШаблонов.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
	               |	ПоляШаблонов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ПоляШаблонов.ДопПараметр КАК ДопПараметр,
	               |	ПоляШаблонов.ТочностьЧисла КАК ТочностьЧисла,
	               |	ПоляШаблонов.РучнаяКорректировка КАК РучнаяКорректировка,
	               |	ПоляШаблонов.АлгоритмРасчета КАК АлгоритмРасчета,
	               |	ПоляШаблонов.Предопределенный КАК Предопределенный,
	               |	ПоляШаблонов.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	               |ИЗ
	               |	Справочник.ЕАС_ПоляФормШаблонов КАК ПоляШаблонов
	               |ГДЕ
	               |	ПоляШаблонов.Ссылка В(&МассивСсылок)
	               |	И ПоляШаблонов.Владелец = &Шаблон";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("Шаблон",       Шаблон);
	
	УстановитьПривилегированныйРежим(Истина);               
	Возврат Запрос.Выполнить();
	
КонецФункции	

// Формирует выборку из результата запроса по подчиненным элементам в справочнике ЕАС_ПоляФормШаблонов
// описание параметров см. в функции ЕАС_ОбработкаШаблонов.ЗапросПолейШаблона
//
// Возвращаемое значение:
//  - ВыборкаИзРезультатаЗапроса - результат выборки
//
Функция ВыборкаПолейШаблона(ТабличныйДокумент, Шаблон, ОписанияОбластейСсылок = Неопределено) Экспорт
	
	ЗапросПолейШаблона = ЗапросПолейШаблона(ТабличныйДокумент, Шаблон, ОписанияОбластейСсылок);
	Возврат ЗапросПолейШаблона.Выбрать();
	
КонецФункции	

// Формирует таблицу значений с полями из результата запроса по подчиненным элементам в справочнике ЕАС_ПоляФормШаблонов
// описание параметров см. в функции ЗапросПолейШаблона
//
// Возвращаемое значение:
//  - ТаблицаЗначений - результат выгрузки запроса + дополнительное поле
//  * ИмяЯчейки - Строка(36) - имя ячейки (формируется по уникальному идентификатору с заменой - на _)
//
Функция ТаблицаПолейШаблона(ТабличныйДокумент, Шаблон, ОписанияОбластейСсылок = Неопределено) Экспорт
	
	ЗапросПолейШаблона = ЗапросПолейШаблона(ТабличныйДокумент, Шаблон, ОписанияОбластейСсылок);
	ТаблицаПолейШаблона = ЗапросПолейШаблона.Выгрузить();
	ТаблицаПолейШаблона.Колонки.Добавить("ИмяЯчейки", Новый ОписаниеТипов("Строка",,, Новый КвалификаторыСтроки(36)));
	Для Каждого СтрокаШаблона Из ТаблицаПолейШаблона Цикл
		СтрокаШаблона.ИмяЯчейки = ЕАС_ОбработкаШаблоновКлиентСерверКопируемый.ИмяЯчейкиПоСсылке(СтрокаШаблона.Ссылка);
	КонецЦикла;	
	ТаблицаПолейШаблона.Индексы.Добавить("ИмяЯчейки");
	Возврат ТаблицаПолейШаблона;
	
КонецФункции 	       

#КонецОбласти
