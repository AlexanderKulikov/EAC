// Модуль написан с учетом того, что он может исполняться как в конфигурации БСХП
//  так и в ЕАОС
// Все изменения требуется синхронизировать в обоих конфигурациях

#Область ПрограммныйИнтерфейс

// Назначает текущую область данных для Источника.
//
// Параметры:
// Источник - СправочникОбъект, ДокументОбъект - объект для назначения текущей области
//
Процедура НазначитьОбластьДанных(Источник) Экспорт
	
	ТекущаяОбластьДанных = ОбластьДанныхДляОбъектов();
	
	МетаданныеИсточника = Источник.Метаданные();
	Если ОбщегоНазначения.ЭтоСправочник(МетаданныеИсточника) 
			Или ОбщегоНазначения.ЭтоДокумент(МетаданныеИсточника) Тогда
		Если Источник.ЭтоНовый() Или Источник.ОбменДанными.Загрузка Тогда
			Источник.ОбластьДанных = ТекущаяОбластьДанных;
		ИначеЕсли Источник.ОбластьДанных <> ТекущаяОбластьДанных Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1 используется в другой области (%2).'"),
			Источник,
			Источник.ОбластьДанных);
			ВызватьИсключение ТекстСообщения;	
		КонецЕсли;
	ИначеЕсли ОбщегоНазначения.ЭтоПланОбмена(МетаданныеИсточника) Тогда
		Если Источник.ЭтотУзел  Тогда
			Источник.ОбластьДанных = 0;
		ИначеЕсли Источник.ЭтоНовый() Тогда
			Источник.ОбластьДанных = ТекущаяОбластьДанных;
		ИначеЕсли Источник.ОбластьДанных <> ТекущаяОбластьДанных Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1 используется в другой области (%2).'"),
			Источник,
			Источник.ОбластьДанных);
			ВызватьИсключение ТекстСообщения;	
		КонецЕсли;
	ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеИсточника) Тогда
		Если МетаданныеИсточника.Имя = "ЕАС_ПрисоединенныеДанные" Тогда
			Владельцы = Новый Массив;
			Для Каждого Запись Из Источник Цикл
				Владельцы.Добавить(Запись.ВладелецДанных);
			КонецЦикла;	
			ОбластиДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Владельцы, "ОбластьДанных");
			Для Каждого КлючЗначение Из ОбластиДанных Цикл
				Если КлючЗначение.Значение <> ТекущаяОбластьДанных Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Владелец таблицы %1 используется в другой области (%2).'"),
						КлючЗначение.Ключ,
						КлючЗначение.Значение);
					ВызватьИсключение ТекстСообщения;	
				КонецЕсли;	
			КонецЦикла;	
		ИначеЕсли МетаданныеИсточника.Имя = "ЕАС_СоответствияОбъектовИнформационныхБаз" Тогда
		ИначеЕсли МетаданныеИсточника.Измерения.Найти("ОбластьДанных") <> Неопределено Тогда
			Для Каждого Запись Из Источник Цикл
				Запись.ОбластьДанных = ТекущаяОбластьДанных;
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

// Возвращает текущую область данных
//
// Возвращаемое значение:
//   Число - текущая область данных
//
Функция ОбластьДанныхДляОбъектов() Экспорт
	
	РежимАдминистрирования = Ложь;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		ЗначениеРазделителяСеанса = МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		Если ЗначениеРазделителяСеанса = 0 
			И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() 
			И Не РежимАдминистрирования Тогда
			ВызватьИсключение НСтр("ru = 'Требуется установить область данных.'");	
		КонецЕсли;	
		ТекущаяОбластьДанных = ЗначениеРазделителяСеанса;
	Иначе	
		ТекущаяОбластьДанных = 0;
	КонецЕсли;
	
	Возврат ТекущаяОбластьДанных;
	
КонецФункции

#КонецОбласти