#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает текстовый документ, содержащий протокол обмена.
//
Функция ПолучитьТекстПротокола(ВидПротокола, Период = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		                      |	Т.Период КАК Период,
		                      |	Т.Протокол КАК Протокол
		                      |ИЗ
		                      |	РегистрСведений.ЕАС_ПротоколыОбмена.СрезПоследних(, ВидПротокола = &ВидПротокола) КАК Т");
		
	Иначе
		
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		                      |	Т.Период КАК Период,
		                      |	Т.Протокол КАК Протокол
		                      |ИЗ
		                      |	РегистрСведений.ЕАС_ПротоколыОбмена КАК Т
		                      |ГДЕ
		                      |	Т.ВидПротокола = &ВидПротокола
		                      |	И Т.Период = &Период");
		
		Запрос.УстановитьПараметр("Период", Период);
	КонецЕсли;	
		
	Запрос.УстановитьПараметр("ВидПротокола", ВидПротокола);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		// Получим протокол в формате текстового документа
		Протокол = Выборка.Протокол.Получить();
		
		Если ТипЗнч(Протокол) <> Тип("ТекстовыйДокумент")  Тогда
			ВызватьИсключение НСтр("ru='Ошибка получения текста протокола обмена.'");
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение НСтр("ru='Протокол обмена не найден. Возможно он был удален.'");
		
	КонецЕсли;
		
	Возврат Протокол;
	
КонецФункции

#КонецОбласти

#КонецЕсли
