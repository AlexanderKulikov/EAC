#Область ОписаниеПеременных

&НаКлиенте
Перем ШтатноеЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ДопПараметрыПолей", ДопПараметрыПолей);      
	КоординатыЯчейки = "S" + Формат(Параметры.Верх, "ЧН=0; ЧГ=0") + ":" + Формат(Параметры.Лево, "ЧН=0; ЧГ=0");
	
	Период.ДатаНачала    = Объект.ДатаНачала;    
	Период.ДатаОкончания = Объект.ДатаОкончания;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзменениеФорматаЧисла();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДатаНачала    = Период.ДатаНачала;
	ТекущийОбъект.ДатаОкончания = Период.ДатаОкончания;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
        Возврат;
    КонецЕсли; 
    
    Если ШтатноеЗакрытие = Неопределено Тогда
        ШтатноеЗакрытие = Истина;
        СтандартнаяОбработка = Ложь;
        Отказ = Истина;
        ПодключитьОбработчикОжидания("ШтатноеЗакрытиеФормы", 0.1, Истина);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ВозвращаемоеЗначение = Объект.Ссылка;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтатноеЗакрытиеФормы() Экспорт
	Закрыть(ВозвращаемоеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПоказательОтчетаПриИзменении(Элемент)
	ФормированиеНаименования();
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Объект.ДатаНачала    = Период.ДатаНачала;
	Объект.ДатаОкончания = Период.ДатаОкончания;
	ФормированиеНаименования();
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	ФормированиеНаименования();
КонецПроцедуры

&НаКлиенте
Процедура ДопПараметрПриИзменении(Элемент)
	ФормированиеНаименования();
КонецПроцедуры


&НаКлиенте
Процедура ТочностьЧислаПриИзменении(Элемент)
	Если ТипЗнч(ОтражениеВОтчете) <> Тип("Число") Тогда
		ОтражениеВОтчете = 0;
	КонецЕсли;	
	ИзменениеФорматаЧисла();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйПараметрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДопПараметрыПолей.Скопировать();  
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныйПараметрАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого ДопПараметрПоля Из ДопПараметрыПолей Цикл
		Если Текст = "" Или Лев(ДопПараметрПоля.Значение, СтрДлина(Текст)) = Текст Тогда
			ДанныеВыбора.Добавить(ДопПараметрПоля.Значение);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактированиеАлгоритма(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", СтрШаблон(НСтр("ru = 'Алгоритм расчета %1'"), Объект.Наименование));
	ПараметрыФормы.Вставить("Текст",     Объект.АлгоритмРасчета);
	ДопПараметрыПолей = Новый Структура;                 
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ОкончаниеРедактированияАлгоритма", ЭтотОбъект, ДопПараметрыПолей);
	ОткрытьФорму("ОбщаяФорма.ЕАС_ФормаРедактированияТекста", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,,ОповещениеЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции    

&НаКлиенте
Процедура ФормированиеНаименования()
	
	Объект.Наименование = КоординатыЯчейки 
					+ " - " + Объект.ПоказательОтчета  
 					+ " - " + Период 
					+ " - " + Объект.Источник;
	Если ЗначениеЗаполнено(Объект.ДопПараметр) Тогда 
		Объект.Наименование = Объект.Наименование + " - " + Объект.ДопПараметр;
	КонецЕсли;	                                     
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеРедактированияАлгоритма(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Объект.АлгоритмРасчета = Результат;
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ИзменениеФорматаЧисла()
	Элементы.ОтражениеВОтчете.ФорматРедактирования = "ЧДЦ=" + Объект.ТочностьЧисла;
КонецПроцедуры	

#КонецОбласти

